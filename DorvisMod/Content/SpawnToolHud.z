class SpawnToolHud : ScriptComponent
{
    [Property]
    var Blocks : ResourceTable = ResourceTable.Find("Blocks");
    
    var Spawnables : Array[Archetype] = new Array[Archetype]();
    
    var SpinAngle : Real = 0.0;
    
    var Icons : Array[Cog] = new Array[Cog]();
    
    var SelectedNonWrappedIndex : Integer = 0;
    var SelectedIndex : Integer = 0;
    
    var ActualAngle : Real = 0.0;
    
    constructor() : base()
    {
        
    }
    
    function Initialize(init : CogInitializer)
    {
        Zero.Connect(this.Space, Events.LogicUpdate, this.OnLogicUpdate);
    }

    function OnAllObjectsCreated()
    {
        var count = this.Blocks.Size;
        for (var i = 0; i < count; ++i)
        {
            var resource = this.Blocks.GetResourceAt(i) as Archetype;
            
            this.Spawnables.Add(resource);
            
            var block = this.Space.Create(resource);
            
            this.Icons.Add(block);
            
            SpawnToolHud.MakeStatic(block);
            block.AttachTo(this.Owner);
            block.Transform.LocalTranslation = local Real3(0.0, 0.0, 0.0);
        }
    }
    
    [Static]
    function MakeStatic(cog : Cog)
    {
        var rigidBody = cog.RigidBody;
        if (rigidBody != null)
        {
            rigidBody.Static = true;
        }
        
        var hierarchy = cog.Hierarchy;
        
        if (hierarchy != null)
        {
            foreach (var child in hierarchy.Children)
            {
                SpawnToolHud.MakeStatic(child);
            }
        }
    }
    
    function ChangeSelection(delta : Integer)
    {
        this.SelectedNonWrappedIndex -= delta;
        this.SelectedIndex = this.SelectedNonWrappedIndex;
        this.SelectedIndex %= this.Icons.Count;
        
        if (this.SelectedIndex < 0)
        {
            this.SelectedIndex += this.Icons.Count;
        }
    }

    function OnLogicUpdate(e : UpdateEvent)
    {
        var count = this.Icons.Count;
        var targetAngle = -((this.SelectedNonWrappedIndex as Real) / count as Real) * Math.Pi * 2.0 + Math.Pi / 2.0;
        this.ActualAngle = Math.Lerp(this.ActualAngle, targetAngle, 0.3);
        
        this.SpinAngle += e.Dt;
        for (var i = 0; i < count; ++i)
        {
            var block = this.Icons.Get(i);
            
            var angle = this.ActualAngle + (i as Real / count as Real) * Math.Pi * 2.0;
            
            var x = Math.Cos(angle) * 5.0;
            var y = Math.Sin(angle) * 2.0 - 1.0;
            
            var targetTranslation = local Real3(x, y, 0.0);
            var currentTranslation = block.Transform.Translation;
            
            //block.Transform.Translation = Math.Lerp(currentTranslation, targetTranslation, 0.1);
            block.Transform.Translation = targetTranslation;
            
            //if (i == this.SelectedIndex)
            //{
            //    block.Model.Material = Material.Find("Caution");
            //}
            //else
            //{
            //    block.Model.Material = Material.Find("Grid");
            //}
        }
        
        //this.Owner.Transform.SetEulerAnglesXYZ(0.0, this.SpinAngle, 0.0);
    }
}
