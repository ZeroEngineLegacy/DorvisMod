class LockStepInitEvent : Event
{
    constructor() : base() {}
    
    var Peer : LockStepPeer = null;
}

class GameStartup : ScriptComponent
{
    var Random : Random = new Random();
    
    var MainSpace : Space = null;
    
    constructor() : base()
    {
        this.Random.Seed(10239);
    }

    function Initialize(init : CogInitializer)
    {
        Zero.Connect(this.Owner, Events.LockStepGameStarted, this.OnLockStepGameStarted);
        
        this.MainSpace = Zero.Game.CreateSpace("Space");
        this.MainSpace.LoadLevel("MainLevel");
    }

    function OnLockStepGameStarted(e : LockStepGameStartEvent)
    {
        foreach (var peer in e.GetPeers())
        {
            var position = this.Random.Real3(0.0, 200.0);
            position.Y = 0.0;
            
            var player = this.MainSpace.Create("Player");
            
            player.Transform.WorldTranslation = position;
            
            if (peer == this.Owner.LockStepGame.GetUnsafeLocalPeer())
            {
                var camViewport = player.FindChildByName("Camera").CameraViewport;
                camViewport.Active = true;
                this.Owner.LockStepMouse.SetInputViewport(camViewport.Viewport);
                this.Owner.LockStepKeyboard.SetInputViewport(camViewport.Viewport);
            }
            
            player.Player.LockStepInitialize(peer);
            
            //Let all components know that lock step is being initialized
            //(so they can grab their peer)
            var toSend = new LockStepInitEvent();
            toSend.Peer = peer;
            player.DispatchEvent("LockStepInit", toSend as Event);
        }
    }
}
