class Chair : ScriptComponent
{
    var ActivePlayerCog : Cog = null;
    
    constructor() : base()
    {
    }

    function Initialize(init : CogInitializer)
    {
        Zero.Connect(this.Owner, "TestInteract", this.OnTestInteract);
        Zero.Connect(this.Owner, "Interact", this.OnInteract);
    }

    function OnTestInteract(e : InteractEvent)
    {
        if (this.ActivePlayerCog == null)
        {
            e.Message = "sit down in the chair";
        }
        else if (e.PlayerCog == this.ActivePlayerCog)
        {
            e.Message = "get out of the chair";
        }
    }

    function OnInteract(e : InteractEvent)
    {
        // Are we trying to get in?
        if (this.ActivePlayerCog == null)
        {
            // The player could already be sitting in a chair, notify anyone of this change
            e.Player.SendPlayerEventTo("ChangeChairs", e.PlayerCog);
            
            e.Player.Deactivate();
            e.Player.RotationAngles = local Real2();
            e.PlayerCog.AttachTo(this.Owner);
            e.PlayerCog.Transform.Translation = local Real3();
            e.PlayerCog.Transform.Rotation = local Quat();
            e.Player.FreezeMouseInput = true;
            
            this.ActivePlayerCog = e.PlayerCog;
            
            Zero.Connect(e.Peer as Cog, Events.LockStepKeyDown, this.OnUserKeyDown);
            Zero.Connect(e.Peer as Cog, Events.LockStepKeyUp, this.OnUserKeyUp);
            Zero.Connect(e.Peer as Cog, Events.LockStepMouseMove, this.OnUserMouseMove);
            
            Zero.Connect(e.PlayerCog, "ChangeChairs", this.OnChangeChairs);
        }
        // Otherwise, we're trying to get out
        else if (e.PlayerCog == this.ActivePlayerCog)
        {
            this.UnseatPlayer(e.PlayerCog, e.Peer);
        }
    }
    
    function OnUserKeyDown(e : LockStepKeyEvent)
    {
        if(e.Key == Keys.Back && this.ActivePlayerCog != null)
        {
            this.UnseatPlayer(this.ActivePlayerCog, this.ActivePlayerCog.Player.Peer);
            return;
        }
        
        var root = this.Owner.GetRoot();
        root.DispatchEvent(Events.LockStepKeyDown, e as Event);
    }
    
    function OnUserKeyUp(e : LockStepKeyEvent)
    {
        var root = this.Owner.GetRoot();
        root.DispatchEvent(Events.LockStepKeyUp, e as Event);
    }
    
    function OnUserMouseMove(e : LockStepMouseEvent)
    {
        var root = this.Owner.GetRoot();
        root.DispatchEvent(Events.LockStepMouseMove, e as Event);
    }
    
    function UnseatPlayer(playerCog : Cog, peer : LockStepPeer)
    {
        playerCog.DetachRelative();
        playerCog.Player.Activate();
        playerCog.Player.FreezeMouseInput = false;
        
        this.ActivePlayerCog = null;
        
        Zero.Disconnect(peer as Cog, Events.LockStepKeyDown, this as Component);
        Zero.Disconnect(peer as Cog, Events.LockStepKeyUp, this as Component);
        Zero.Disconnect(peer as Cog, Events.LockStepMouseMove, this as Component);
        Zero.Disconnect(playerCog, "ChangeChairs", this as Component);
    }
    
    function OnChangeChairs(e : PlayerEvent)
    {
        this.UnseatPlayer(e.PlayerCog, e.Peer);
    }
}
