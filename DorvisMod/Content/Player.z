class Player : ScriptComponent
{
    [Editable][Serialized]
    var Speed : Real = 10.0;
    
    [Editable][Serialize]
    var LookSpeed : Real = 1.0;
    
    [Editable][Serialize]
    var JumpVelocity : Real = 6.0;
    
    [Editable][Serialize]
    var InteractDistance : Real = 6.0;
    
    [Editable][Serialize]
    var Active : Boolean = true;
    
    [Editable][Serialize][SoundCue]
    var InteractSound : String = "Interact";
    [Editable][Serialize][SoundCue]
    var ChangeToolSound : String = "ChangeTool";
    
    
    var Peer : LockStepPeer = null;
    var CameraCog : Cog = null;
    var ExtraCollision : Cog = null;
    
    var RotationAngles : Real2 = local Real2(0.0, 0.0);
    var ToolContainer : Cog = null;
    var Id : Integer = 0;
    var UndoCounter : Integer = 0;
    
    var Movement : Real3 = local Real3();
    
    var Hud : Space = null;
    var HudContextText : ShadowText = null;
    var HudToolText : ShadowText = null;
    var HudToolSubText : ShadowText = null;
    var HudUndoText : ShadowText = null;
    
    var InteractMessage : String = null;
    var InteractCog : Cog = null;
    
    var Tools : Array[Cog] = new Array[Cog]();
    var ActiveTool : Cog = null;
    
    var FreezeInput : Boolean = false;
    
    // Only used for client side effects that have absolutely no baring on gameplay
    var UnsafeIsLocal : Boolean = false;
    
    
    // For some reason this results in a type not found error
    //var UndoStack : Array[delegate()] = new Array[delegate()]();
    var UndoStack : Array[Undo] = new Array[Undo]();
    
    constructor() : base()
    {
    }

    function Initialize(init : CogInitializer)
    {
    }
    
    function LockStepInitialize(peer : LockStepPeer, id : Integer, unsafeIsLocal : Boolean)
    {
        this.UnsafeIsLocal = unsafeIsLocal;
        this.Id = id;
        this.Peer = peer;
        this.CameraCog = this.Owner.FindChildByName("Camera");
        this.ExtraCollision = this.Owner.FindChildByName("ExtraCollision");
        
        Zero.Connect(this.Space as Cog, Events.LogicUpdate, this.OnLogicUpdate);
        Zero.Connect(peer as Cog, Events.LockStepMouseMove, this.OnLockStepMouseMove);
        Zero.Connect(peer as Cog, Events.LockStepMouseDown, this.OnLockStepMouseDown);
        Zero.Connect(peer as Cog, Events.LockStepMouseUp, this.OnLockStepMouseUp);
        Zero.Connect(peer as Cog, Events.LockStepKeyDown, this.OnLockStepKeyDown);
        Zero.Connect(peer as Cog, Events.LockStepKeyUp, this.OnLockStepKeyUp);
        
        this.ToolContainer = this.Owner.FindChildByName("ToolContainer");
        
        this.Hud = Zero.Game.CreateSpace("DefaultSpace");
        this.Hud.GraphicsSpace.SpriteSmoothing = true;
        this.Hud.GraphicsSpace.PixelSnapping = true;
        this.Hud.LoadLevel("Hud");
        this.HudContextText = this.Hud.FindObjectByName("ContextText").ShadowText;
        this.HudToolText = this.Hud.FindObjectByName("ToolText").ShadowText;
        this.HudToolSubText = this.Hud.FindObjectByName("ToolSubText").ShadowText;
        this.HudUndoText = this.Hud.FindObjectByName("UndoText").ShadowText;
        
        // Add all the initial tools
        foreach (var tool in  this.ToolContainer.Hierarchy.GetChildren())
        {
            this.Tools.Add(tool);
        }
        
        // Call this to sign up for most events, as well as setup the first tool
        this.ActiveTool = this.Tools.Get(0);
        this.Activate();
    }
    
    function Destroyed()
    {
        if (this.Hud != null)
        {
            this.Hud.Destroy();
        }
    }
    
    function Activate()
    {
        this.Owner.RigidBody.Static = false;
        this.Owner.Collider.Ghost = false;
        this.ExtraCollision.Collider.Ghost = false;
        this.Owner.DynamicMotor.Activate();
        this.Active = true;
        this.ActivateTool(this.ActiveTool);
        
        // Because when activating, we could be inside something, attempt to resolve a portion of the collision
        foreach (var contact in this.Owner.Collider.GetContacts())
        {
            this.Owner.Transform.WorldTranslation -= contact.FirstPoint.WorldNormalTowardsOther * local Real3(0.5);
        }
    }
    
    function Deactivate()
    {
        this.Owner.RigidBody.Static = true;
        this.Owner.Collider.Ghost = true;
        this.ExtraCollision.Collider.Ghost = true;
        this.Owner.DynamicMotor.Deactivate();
        this.Active = false;
        this.DeactivateTool();
        this.HudContextText.Text = "";
    }
    
    function QueueUndo(name : String, action : delegate(undo : Undo) : Boolean) : Undo
    {
        var undo = this.QueueUndo(name);
        undo.Action = action;
        return undo;
    }
    
    function QueueUndo(name : String) : Undo
    {
        var undo = new Undo();
        undo.Name = name;
        ++this.UndoCounter;
        
        var builder = new StringBuilder();
        builder.Write(this.Id);
        builder.Write("_");
        builder.Write(this.UndoCounter);
        
        undo.UniqueName = builder.ToString();
        this.UndoStack.Add(undo);
        
        this.HudUndoText.Text = String.Concatenate("Press [Z] to undo ", name);
        
        return undo;
    }
    
    function AddMovement(e : LockStepKeyEvent, key : Integer, movement : Real3)
    {
        if (e.Key == key)
        {
            this.Movement += movement;
        }
    }
    
    function OnLogicUpdate(e : UpdateEvent)
    {
        var playerRotation = local Real3(0.0, this.RotationAngles.Y, 0.0);
        var cameraRotation = local Real3(this.RotationAngles.X, Math.Pi, 0.0);
        this.Owner.Transform.SetRotationAngles(playerRotation);
        this.CameraCog.Transform.SetRotationAngles(cameraRotation);
        
        if (this.FreezeInput == false)
        {
            var movement = this.Owner.Transform.TransformNormal(this.Movement);
            movement.Y = 0.0;
            //this.Owner.DynamicMotor.SetMaxMoveForce(1.0);
            this.Owner.DynamicMotor.MoveInDirection(movement * local Real3(this.Speed), local Real3(0.0, 1.0, 0.0));
        }
        
        this.InteractCog = null;
        this.HudContextText.Text = "";
        
        // Only do interactions if the player is active
        this.InteractMessage = null;
        var result = this.RayCast(this.InteractFilter, this.InteractDistance);
        
        if (result != null)
        {
            this.InteractCog = result.Object;
            this.HudContextText.Text = String.Concatenate("Press [E] to ", this.InteractMessage);
        }
    }
    
    function SendInteractEvent(eventName : String, to : Cog) : InteractEvent
    {
        var toSend = new InteractEvent();
        toSend.PlayerCog = this.Owner;
        toSend.Player = this;
        toSend.Peer = this.Peer;
        to.DispatchEvent(eventName, toSend as Event);
        return toSend;
    }
    
    function InteractFilter(hit : Cog) : Boolean
    {
        var e = this.SendInteractEvent("TestInteract", hit);
        this.InteractMessage = e.Message;
        
        // Commenting this in will cause a crash... WHY? (because it's null!)
        //Console.WriteLine(e.Message);
        return e.Message != null;
    }
    
    function RayCast(filter : delegate(hit : Cog) : Boolean) : HitResult
    {
        return this.RayCast(filter, 100000.0);
    }
    
    function RayCast(filter : delegate(hit : Cog) : Boolean, maxDistance : Real) : HitResult
    {
        var dir = this.CameraCog.Orientation.GetWorldForward();
        var start = this.CameraCog.Transform.WorldTranslation;
        
        var foundObject : Cog = null;
        var worldNormal = local Real3();
        var worldPosition = local Real3();
        
        var castResults = this.Space.PhysicsSpace.CastRayResults(start, dir, 20);
        foreach(var castResult in castResults)
        {
            if (castResult.GetDistance() > maxDistance)
            {
                break;
            }
            
            var hitObject = castResult.GetObjectHit();
            
            // Skip ourself
            if (hitObject.GetRoot() == this.Owner)
            {
                continue;
            }
            
            if (filter(hitObject))
            {
                foundObject = hitObject;
                worldNormal = castResult.GetNormal();
                worldPosition = castResult.GetWorldPosition();
                break;
            }
            
        }
        
        var nullResult : HitResult = null;
        
        if(foundObject == null)
        {
            return nullResult;
        }
        
        var result = new HitResult();
        result.Object = foundObject;
        result.WorldNormal = worldNormal;
        result.WorldPosition = worldPosition;
        
        return result;
    }
    
    function OnLockStepMouseMove(e : LockStepMouseEvent)
    {
        this.RotationAngles.Y -= e.ScreenDelta.X * this.LookSpeed;
        this.RotationAngles.X -= e.ScreenDelta.Y * this.LookSpeed;
        
        var maxDown = -Math.Pi / 2.0;
        var maxUp = Math.Pi / 2.0;
        
        if (this.RotationAngles.X < maxDown)
        {
            this.RotationAngles.X = maxDown;
        }
        
        if (this.RotationAngles.X > maxUp)
        {
            this.RotationAngles.X = maxUp;
        }
    }
    
    function OnLockStepMouseDown(e : LockStepMouseButtonEvent)
    {
    }
    
    function OnLockStepMouseUp(e : LockStepMouseButtonEvent)
    {
    }
    
    function OnLockStepKeyDown(e : LockStepKeyEvent)
    {
        this.AddMovement(e, Keys.W, local Real3( 0.0, 0.0,  1.0));
        this.AddMovement(e, Keys.S, local Real3( 0.0, 0.0, -1.0));
        this.AddMovement(e, Keys.A, local Real3( 1.0, 0.0,  0.0));
        this.AddMovement(e, Keys.D, local Real3(-1.0, 0.0,  0.0));
        
        // We still allow movement to happen when freezing input (we ignore it in update)
        if (this.FreezeInput)
        {
            return;
        }
        
        if (this.Active)
        {
            if(e.Key == Keys.Space)
            {
                this.Owner.RigidBody.ApplyLinearVelocity(local Real3(0.0, this.JumpVelocity, 0.0));
            }
            
            var index = e.Key - Keys.One;
            
            // Handle the '0' button case
            if(index == -1)
            {
                index = 9;
            }
            
            // Check if the key was a valid key
            if(index >= 0 && index < this.Tools.Count)
            {
                var newTool = this.Tools.Get(index);
                if(this.ActiveTool != newTool)
                {
                    this.DeactivateTool();
                    this.ActivateTool(newTool);
                }
            }
        }
        
        if(e.Key == Keys.Z && this.UndoStack.Count != 0)
        {
            var undo = this.UndoStack.Pop();
            var result = undo.Action(undo);
            
            // If we failed an undo, just clear the undo stack for now
            if (result == false)
            {
                this.UndoStack.Clear();
            }
            
            // If that was the last undo...
            if (this.UndoStack.Count == 0)
            {
                this.HudUndoText.Text = "";
            }
        }
        
        if(e.Key == Keys.E && this.InteractCog != null)
        {
            this.Space.SoundSpace.PlayCueAt(this.InteractSound, this.InteractCog.Transform.WorldTranslation);
            this.SendInteractEvent("Interact", this.InteractCog);
        }
    }
    
    function OnLockStepKeyUp(e : LockStepKeyEvent)
    {
        this.AddMovement(e, Keys.W, local Real3( 0.0, 0.0, -1.0));
        this.AddMovement(e, Keys.S, local Real3( 0.0, 0.0,  1.0));
        this.AddMovement(e, Keys.A, local Real3(-1.0, 0.0,  0.0));
        this.AddMovement(e, Keys.D, local Real3( 1.0, 0.0,  0.0));
    }
    
    
    function DeactivateTool()
    {
        this.SendToolEvent("DeactivateTool", this.ActiveTool);
        this.HudToolText.Text = "";
        this.HudToolSubText.Text = "";
    }
    
    function ActivateTool(tool : Cog)
    {
        this.HudToolText.Text = tool.Name;
        this.HudToolSubText.Text = "";
        this.ActiveTool = tool;
        this.SendToolEvent("ActivateTool", this.ActiveTool);
        this.Space.SoundSpace.PlayCue(this.ChangeToolSound);
    }
    
    function SendToolEvent(eventName : String, obj : Cog)
    {
        var toSend = new ToolChangeEvent();
        toSend.Peer = this.Peer;
        toSend.CameraCog = this.CameraCog;
        toSend.PlayerCog = this.Owner;
        toSend.Player = this;
        toSend.HudText = this.HudToolSubText;
        obj.DispatchEvent(eventName, toSend as Event);
    }
    
    function SendPlayerEventTo(eventName : String, to : Cog)
    {
        var toSend = new PlayerEvent();
        toSend.Player = this;
        toSend.PlayerCog = this.Owner;
        toSend.Peer = this.Peer;
        to.DispatchEvent(eventName, toSend as Event);
    }
}

class HitResult
{
    var Object : Cog = null;
    var WorldNormal : Real3 = local Real3();
    var WorldPosition : Real3 = local Real3();
    
    constructor()
    {
    }
}

class InteractEvent : Event
{
    // If this gets set to anything but null, it means someone responded
    var Message : String = null;
    
    var PlayerCog : Cog = null;
    var Player : Player = null;
    var Peer : LockStepPeer = null;
    
    constructor() : base()
    {
    }
}

class ToolChangeEvent : Event
{
    var Index : Integer = 0;
    var LastIndex : Integer = 0;
    var Peer : LockStepPeer = null;
    var CameraCog : Cog = null;
    var PlayerCog : Cog = null;
    var Player : Player = null;
    var HudText : ShadowText = null;
    
    constructor() : base() {}
}

class Undo
{
    var Action : delegate(undo : Undo) : Boolean = Undo.DoNothing;
    var UniqueName : String = "";
    var Name : String = "Unknown";
    
    [Static]
    function DoNothing(undo : Undo) : Boolean
    {
        return false;
    }
    
    constructor()
    {
    }
}

class PlayerEvent : Event
{
    var Player : Player = null;
    var PlayerCog : Cog = null;
    var Peer : LockStepPeer = null;
    
    constructor() : base()
    {
    }
}

