class CommandBlock : ScriptComponent
{
    var ReDirectionCog : Cog = null;
    var Root : Cog = null;
    
    constructor() : base()
    {
    }
    
    function Initialize(init : CogInitializer)
    {
        Zero.Connect(this.Space as Cog, Events.LogicUpdate, this.OnLogicUpdate);
    }

    function OnLogicUpdate(e : UpdateEvent)
    {
        if(this.ReDirectionCog == null)
        {
            return;
        }
        
        var start = this.Owner.Transform.WorldTranslation;
        var end = this.ReDirectionCog.Transform.WorldTranslation;
        
        var line = new DebugLine();
        line.Start(start);
        line.End(end);
        line.HeadSize(0.2);
        line.Draw();
    }
    
    // Setup can be called multiple times, but it MUST be called any time our hierarchy is changed
    function Setup()
    {
        var root = this.Owner.GetRoot();
        
        if (this.Root != null)
        {
            Zero.Disconnect(this.Root, "PlayerKeyDown", this as Component);
            Zero.Disconnect(this.Root, "PlayerKeyUp", this as Component);
        }
        
        Zero.Connect(root, "PlayerKeyDown", this.OnKeyDown);
        Zero.Connect(root, "PlayerKeyUp", this.OnKeyUp);
        
        this.Root = root;
    }
    
    function OnKeyDown(e : PlayerKeyEvent)
    {
        if(this.ReDirectionCog != null)
        {
            this.ReDirectionCog.DispatchEvent("PlayerKeyDown", e as Event);
        }
    }
    
    function OnKeyUp(e : PlayerKeyEvent)
    {
        if(this.ReDirectionCog != null)
        {
            this.ReDirectionCog.DispatchEvent("PlayerKeyUp", e as Event);
        }
    }
}
