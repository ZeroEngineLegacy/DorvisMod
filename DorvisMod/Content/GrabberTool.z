class GrabberTool : ScriptComponent
{
    //If this is set then the manipulation joint created
    //will lock the rotation of the grabbed object.
    [Editable][Serialized]
    var LockRotation : Boolean = true;
    
    //The minimum distance we can drag the object towards
    //the camera during mouse scroll.
    [Editable][Serialized]
    var MinDistance : Real = 3.0;
    
    //The maximum distance we can drag the objet away from the camera.
    [Editable][Serialized]
    var MaxDistance : Real = 100.0;
    
    //We store the viewport so we can do raycasts
    var GameViewport : GameViewport = null;
    //The object that we are currently grabbing
    var SelectedObj : Cog = null;
    //The current distance away from the camera that
    //the object's grab point should be.
    var Distance : Real = 0.0;
    
    var ManipulatorJoint : ManipulatorJoint = null;
    var PhyGunJoint : PhyGunJoint = null;
    
    var Index : Integer = 0;
    
    var CameraCog : Cog = null;
    
    constructor() : base()
    {
    }

    function Initialize(init : CogInitializer)
    {
        //we only operator on a component with a camera viewport,
        //grab the viewport from that component
        //this.GameViewport = this.Owner.CameraViewport.Viewport;
        //
        Zero.Connect(this.Owner, "ActivateTool", this.OnActivateTool);
        Zero.Connect(this.Owner, "DeactivateTool", this.OnDeactivateTool);
    }
    
    function OnActivateTool(e : ToolChangeEvent)
    {
        Console.WriteLine(e.CameraCog == null);
        //this.GameViewport = this.Owner.CameraViewport.Viewport;
        this.CameraCog = e.CameraCog;
        Zero.Connect(e.Peer as Cog, Events.LockStepMouseMove, this.OnLockStepMouseMove);
        Zero.Connect(e.Peer as Cog, Events.LockStepMouseDown, this.OnLockStepMouseDown);
        Zero.Connect(e.Peer as Cog, Events.LockStepMouseUp, this.OnLockStepMouseUp);
        Zero.Connect(e.Peer as Cog, Events.LockStepKeyDown, this.OnLockStepKeyDown);
        //Zero.Connect(peer as Cog, Events.MouseScroll, this.OnMouseScroll);
        //Zero.Connect(peer as Cog, Events.LockStepRightMouseDown, this.OnLockStepRightMouseDown);
    }
    
    function OnDeactivateTool(e : ToolChangeEvent)
    {
        Zero.Disconnect(e.Peer as Cog, Events.LockStepMouseMove, this as Component);
        Zero.Disconnect(e.Peer as Cog, Events.LockStepMouseDown, this as Component);
        Zero.Disconnect(e.Peer as Cog, Events.LockStepMouseUp, this as Component);
        Zero.Disconnect(e.Peer as Cog, Events.LockStepKeyDown, this as Component);
    }
    
    function OnLockStepKeyDown(e : LockStepKeyEvent)
    {
        //we only do logic when 'P' is pressed right now
        if(e.Key != Keys.P)
        {
            return;
        }
        
        //if we have an object selected, we need to
        //deselect it before changing our joint type
        if(this.SelectedObj != null)
        {
            this.DeselectObj();
        }
        this.LockRotation = !this.LockRotation;
    }
    
    function OnLockStepMouseMove(e : LockStepMouseEvent)
    {
        this.UpdateDraggedObject(e);
    }
    
    function FreezeObject(e : LockStepMouseButtonEvent)
    {
        if(this.SelectedObj != null)
        {
            if(this.SelectedObj.RigidBody != null)
            {
                this.SelectedObj.RigidBody.Static = !this.SelectedObj.RigidBody.Static;
            }
        }
        else
        {
            var castResults = this.Space.PhysicsSpace.CastRayResults(e.WorldStart, e.WorldDirection, 1);
            foreach(var castResult in castResults)
            {
                var obj = castResult.GetObjectHit();
                if(obj == null)
                {
                    return;
                }
                
                if(obj.RigidBody != null)
                {
                    obj.RigidBody.Static = !obj.RigidBody.Static;
                }
            }
        }
    }
    
    function GrabObject(e : LockStepMouseButtonEvent)
    {
        var dirW = this.CameraCog.Orientation.GetWorldForward();
        var posW = this.CameraCog.Transform.WorldTranslation;
        
        var debugLine = new DebugLine();
        debugLine.Start(posW).End(posW + dirW).Duration(10.0).Float4Color(local Real4(1.0, 1.0, 1.0, 1.0));
        debugLine.Draw();
        
        var debugSphere = new DebugSphere();
        debugSphere.Position(posW + dirW).Radius(0.1).Duration(10.0);
        debugSphere.Draw();
        
        
        if(this.SelectedObj != null)
        {
            this.DeselectObj();
        }
        
        var castResults = this.Space.PhysicsSpace.CastRayResults(posW, dirW, 2);
        foreach(var castResult in castResults)
        {
            var obj = castResult.GetObjectHit();
            
            if(obj.GetRoot() == this.Owner.GetRoot())
            {
                continue;
            }
            
            this.SelectedObj = obj;
            if(this.SelectedObj == null)
            {
                return;
            }
            
            if(this.SelectedObj.RigidBody != null)
            {
                this.SelectedObj.RigidBody.ForceAwake();
            }
            
            //find the point at which we clicked the object
            var selectedPoint = castResult.GetWorldPosition();
            //find how far away the pick point is (for keeping the object on a sphere around us)
            this.Distance = Math.Length(selectedPoint - this.Owner.Transform.Translation);
            
            if(this.LockRotation)
            {
                this.CreatePhyGun(this.SelectedObj,selectedPoint);
            }
            else
            {
                this.CreateManipulator(this.SelectedObj,selectedPoint);
            }
            break;
        }
    }
    
    function OnLockStepMouseDown(e : LockStepMouseButtonEvent)
    {
        if(e.RightPressed)
        {
            this.FreezeObject(e);
        }
        else
        {
            this.GrabObject(e);
        }
    }
    
    function OnLockStepMouseUp(e : LockStepMouseButtonEvent)
    {
        this.DeselectObj();
    }
    
    function DeselectObj()
    {
        if(this.SelectedObj == null)
        {
            return;
        }
        
        //mark that we have no selected object, and destroy the joint
        this.SelectedObj = null;
        if(this.LockRotation)
        {
            this.PhyGunJoint.Owner.Destroy();
        }
        else
        {
            this.ManipulatorJoint.Owner.Destroy();
        }
        
        this.ManipulatorJoint = null;
        this.PhyGunJoint = null;
    }
    
    function OnMouseScroll(e : LockStepMouseEvent)
    {
        ////if we do not have a selected object, there is nothing to drag
        //if(this.SelectedObj == null)
        //{
        //    return;
        //}
        //
        ////alter our grab distance but clamp between our min and max distance
        //this.Distance += e.Scroll.Y;
        //this.Distance = Math.Clamp(this.Distance, this.MinDistance, this.MaxDistance);
        ////now compute the new world position of the mouse
        //var newWorldPoint = this.ComputeWorldPoint(e);
        ////update the object we grabbed with the new world point
        //if(this.LockRotation)
        //{
        //    this.UpdatePhyGunPoint(newWorldPoint);
        //}
        //else
        //{
        //    this.UpdateManipulatorPoint(newWorldPoint);
        //}
    }
    
    function CreateManipulator(obj : Cog, point : Real3)
    {
        //build a new manipulator joint attached by the picked point and save it
        var jointCreator = Zero.JointBuilder.CreateInstance();
        jointCreator.ConnectToWorld = true;
        var cog = jointCreator.AttachWorld(obj,obj,point,"ManipulatorJoint");
        this.ManipulatorJoint = cog.ManipulatorJoint;
    }
        
    function CreatePhyGun(obj : Cog, point : Real3)
    {
        //build a new manipulator joint attached by the picked point and save it
        var jointCreator = Zero.JointBuilder.CreateInstance();
        jointCreator.ConnectToWorld = true;
        var cog = jointCreator.AttachWorld(obj,obj,point,"PhyGunJoint");
        this.PhyGunJoint = cog.PhyGunJoint;
    }
        
    function UpdateDraggedObject(e : LockStepMouseEvent)
    {
        //if we do not have a selected object, there is nothing to drag
        if(this.SelectedObj == null)
        {
            return;
        }
        
        var newWorldPoint = this.ComputeWorldPoint(e);
        
        if(this.LockRotation)
        {
            this.UpdatePhyGunPoint(newWorldPoint);
        }
        else
        {
            this.UpdateManipulatorPoint(newWorldPoint);
        }
    }
    
    function ComputeWorldPoint(e : LockStepMouseEvent) : Real3
    {
        //get the vector into the screen from where we clicked
        var viewPlaneNormal = this.CameraCog.Orientation.GetWorldForward();
        //we want to keep the point on the object we clicked at a fixed distance from us,
        //so use the viewPlaneNormal to find the point on the sphere defined by the
        //initial picking distance
        return this.CameraCog.Transform.WorldTranslation + viewPlaneNormal * local Real3(this.Distance);
    }
        
    function UpdateManipulatorPoint(worldPoint : Real3)
    {
        this.ManipulatorJoint.SetTarget(worldPoint);
    }
        
    function UpdatePhyGunPoint(worldPoint : Real3)
    {
        this.PhyGunJoint.SetTargetPoint(worldPoint);
    }
}
