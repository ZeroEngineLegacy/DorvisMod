class SpawnTool : ScriptComponent
{
    var GameViewport : GameViewport = null;
    var Index : Integer = 0;
    var CameraCog : Cog = null;
    var Player : Player = null;
    var Ghost : DebugObb = new DebugObb();
    var IsOverBlock : Boolean = false;
    
    constructor() : base()
    {
    }

    function Initialize(init : CogInitializer)
    {
        Zero.Connect(this.Owner, "ActivateTool", this.OnActivateTool);
        Zero.Connect(this.Owner, "DeactivateTool", this.OnDeativateTool);
    }
    
    function OnActivateTool(e : ToolChangeEvent)
    {
        this.CameraCog = e.CameraCog;
        this.Player = e.Player;
        Zero.Connect(e.Peer as Cog, Events.LockStepMouseDown, this.LockStepMouseDown);
        Zero.Connect(e.Peer as Cog, Events.LockStepMouseMove, this.LockStepMouseMove);
        Zero.Connect(this.Space as Cog, Events.LogicUpdate, this.OnLogicUpdate);
    }
    
    function OnDeativateTool(e : ToolChangeEvent)
    {
        this.CameraCog = null;
        this.Player = null;
        Zero.Disconnect(e.Peer as Cog, Events.LockStepMouseDown, this as Component);
        Zero.Disconnect(e.Peer as Cog, Events.LockStepMouseMove, this as Component);
        Zero.Disconnect(this.Space as Cog, Events.LogicUpdate, this as Component);
    }
    
    function OnLogicUpdate(e : UpdateEvent)
    {
        if (this.IsOverBlock)
        {
            this.Ghost.Draw();
        }
    }
    
    function LockStepMouseDown(e : LockStepMouseButtonEvent)
    {
        if(e.ShiftPressed)
        {
            this.DestroyObj(e);
        }
        else
        {
            this.SpawnObj(e, "Block");
        }
    }
    
    function BlockFilter(hit : Cog) : Boolean
    {
        return hit.BlockScript != null;
    }
    
    function LockStepMouseMove(e : LockStepMouseEvent)
    {
        this.IsOverBlock = false;
        var result = this.Player.RayCast(this.BlockFilter);
        
        if (result == null)
        {
            return;
        }
        
        this.IsOverBlock = true;
        
        var pos = result.Object.Transform.WorldTranslation + result.WorldNormal;
        
        this.Ghost.Float4Color(local Real4(1.0));
        this.Ghost.Extents(local Real3(0.5));
        this.Ghost.Rotation(result.Object.Transform.WorldRotation);
        this.Ghost.Position(pos);
    }
    
    function SpawnObj(e : LockStepMouseButtonEvent, objToSpawn : String)
    {
        var result = this.Player.RayCast(this.BlockFilter);
        
        if (result == null)
        {
            return;
        }
        
        var localNormal = result.Object.Transform.TransformNormalInverse(result.WorldNormal);
        var newObjPos = localNormal;
        
        var newObj = this.Space.CreateAtPosition(objToSpawn, newObjPos);
        newObj.RigidBody.Static = true;
        newObj.AttachTo(result.Object);
    }
    
    function DestroyObj(e : LockStepMouseButtonEvent)
    {
        var result = this.Player.RayCast(this.BlockFilter);
        
        if (result == null)
        {
            return;
        }
        
        result.Object.Destroy();
    }
}
